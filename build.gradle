plugins {
  id "com.jfrog.bintray" version "1.8.4" apply false
}

group 'com.logcapture'
version '1.2.1'
description = 'A testing library for assert logging messages.'

repositories {
  jcenter()
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'java-library'
  apply plugin: "jacoco"

  repositories {
    jcenter()
  }

  test {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  java {
    withJavadocJar()
    withSourcesJar()
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      html.enabled true
    }
  }

  def pomConfig = {
    name project.name
    description project.description
    url "http://www.jspcore.com/"

    licenses {
      license {
        name "MIT License"
        url "https://opensource.org/licenses/MIT"
        distribution "repo"
      }
    }
    scm {
      connection "scm:https://mustaine@github.com/mustaine/logcapture.git"
      developerConnection "scm:git@github.com:mustaine/logcapture.git"
      url "https://github.com/mustaine/logcapture"
    }
    developers {
      developer {
        id "jspcore"
        name "Team JSPCore"
      }
    }
  }

  publishing {
    publications {
      MyPublication(MavenPublication) {
        from components.java
        groupId 'com.logcapture'
        artifactId project.name
        version "1.2.1"

        pom.withXml {
          def root = asNode()
          asNode().appendNode('description', project.description)
          root.children().last() + pomConfig
        }
      }
    }
  }

  apply plugin: 'com.jfrog.bintray'

  bintray {
//  dryRun = true
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['MyPublication']
    pkg {
      repo = 'maven'
      name = project.name
      userOrg = 'jspcore'
      licenses = ['MIT']
      vcsUrl = 'https://github.com/jsalinaspolo/logcapture.git'
      version {
        name = "1.2.1"
        vcsTag = "1.2.1"
        released = new Date()
      }
    }
  }
}

wrapper {
  gradleVersion = '6.7'
  distributionType = Wrapper.DistributionType.ALL
}


