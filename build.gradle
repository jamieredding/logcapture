plugins {
  id "com.jfrog.bintray" version "1.8.4"
}

group 'com.jspcore'
version '1.0.0-SNAPSHOT'
description = 'A testing library for assert logging messages.'

repositories {
  jcenter()
}

subprojects {
  apply plugin: 'idea'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'
  apply plugin: 'java-library'
  apply plugin: "jacoco"

  repositories {
    jcenter()
  }

  test {
    testLogging {
      events "passed", "skipped", "failed"
    }
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  java {
    withJavadocJar()
    withSourcesJar()
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      html.enabled true
    }
  }

  publishing {
    publications {
      MyPublication(MavenPublication) {
        from components.java
        groupId 'com.jspcore'
        artifactId 'logcapture'
        version '1.0.0'

        pom.withXml {
          def root = asNode()
          asNode().appendNode('description', project.description)
          root.children().last() + pomConfig
        }
      }
    }
  }
}

def pomConfig = {
  name project.name
  description project.description
  url "http://www.jspcore.com/"

  licenses {
    license {
      name "MIT License"
      url "https://opensource.org/licenses/MIT"
      distribution "repo"
    }
  }
  scm {
    connection "scm:https://mustaine@github.com/mustaine/logcapture.git"
    developerConnection "scm:git@github.com:mustaine/logcapture.git"
    url "https://github.com/mustaine/logcapture"
  }
  developers {
    developer {
      id "jspcore"
      name "Team JSPCore"
    }
  }
}

bintray {
//  dryRun = true
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

  publications = ['MyPublication']
  pkg {
    repo = 'maven'
    name = 'logcapture'
    userOrg = 'jspcore'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/mustaine/logcapture.git'
    version {
      name = '1.0.0'
      vcsTag = '1.0.0'
      released = new Date()
    }
  }
}

wrapper {
  gradleVersion = '6.3'
  distributionType = Wrapper.DistributionType.ALL
}


